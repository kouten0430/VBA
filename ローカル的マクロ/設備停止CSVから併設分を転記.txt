Sub 設備停止CSVから併設分を転記()
    '転記元と転記先のブックを開いておく
    '転記先の該当月のシートを選択しておく
    '転記元のブックをアクティブにし、カレントリージョンが効く位置でマクロを実行する
    '電気所と転記先の行は必要に応じて変更して下さい
    '転記先の行が合っているか注意！
    'Cellsメソッドの失敗を防ぐため、転記元のCellsはオブジェクトを省略せずに明示してます
    'ローカル的なマクロです
    Dim 行始 As Long
    Dim 行終 As Long
    Dim 列始 As Long
    Dim 列終 As Long
    Dim i As Long
    Dim j As Long
    Dim k As Long
    Dim 開始日時の列番号 As Long
    Dim 終了日時の列番号 As Long
    Dim 実施箇所の列番号 As Long
    Dim 作業内容の列番号 As Long
    Dim 要求箇所の列番号 As Long
    Dim 電気所 As Variant
    Dim 開始日 As Date
    Dim 終了日 As Date
    Dim 作業内容 As String
    Dim 転記元ワークブック名 As String
    Dim 転記先ワークブック名 As String
    Dim 月行 As Long
    Dim 月初 As Long
    Dim 月末 As Long
    Dim 転記先の行 As Variant
    
    Application.ScreenUpdating = False
    
    電気所 = Array("和田川第一発電所", "和田川第二発電所", "亀谷発電所", "松ノ木発電所", "称名川第二発電所", "有峰第三発電所", "小俣発電所", "上滝発電所", "常願寺川第一発電", "常願寺川第三発電")
    転記先の行 = Array(74, 75, 76, 77, 78, 79, 80, 81, 82, 83)  '電気所の並びと要素数を一致させる
    転記元ワークブック名 = ActiveWorkbook.Name
    転記先ワークブック名 = "2020月間業務計画（保守課）.xlsm"
    月行 = 8    '転記先の日付が入っている行番号
    月初 = 5    '転記先の月初の日付が入っている列番号
    月末 = 37   '転記先の月末の日付が入っている列番号

    '---ここから行番号・列番号を取得する処理---
    
    行始 = ActiveCell.CurrentRegion.Row
    行終 = ActiveCell.CurrentRegion.Rows(ActiveCell.CurrentRegion.Rows.Count).Row
    列始 = ActiveCell.CurrentRegion.Column
    列終 = ActiveCell.CurrentRegion.Columns(ActiveCell.CurrentRegion.Columns.Count).Column
    
    For i = 列始 To 列終
        If Cells(行始, i).Value = "調整期間　開始日時" Then
            開始日時の列番号 = i
        ElseIf Cells(行始, i).Value = "調整期間　終了日時" Then
            終了日時の列番号 = i
        ElseIf Cells(行始, i).Value = "実施箇所" Then
            実施箇所の列番号 = i
        ElseIf Cells(行始, i).Value = "作業内容" Then
            作業内容の列番号 = i
        ElseIf Cells(行始, i).Value = "要求箇所" Then
            要求箇所の列番号 = i
        End If
    Next i
    
    '---ここから該当電気所のデータを転記する処理---
    
    k = 0

    Do While k <= UBound(電気所)
        For i = 行始 + 1 To 行終
            If Workbooks(転記元ワークブック名).Sheets(1).Cells(i, 実施箇所の列番号).Value = 電気所(k) And Workbooks(転記元ワークブック名).Sheets(1).Cells(i, 要求箇所の列番号).Value Like "富[変電]保守" Then
                開始日 = WorksheetFunction.Floor(Workbooks(転記元ワークブック名).Sheets(1).Cells(i, 開始日時の列番号).Value, 1)   '時分は切り捨てる
                終了日 = WorksheetFunction.Floor(Workbooks(転記元ワークブック名).Sheets(1).Cells(i, 終了日時の列番号).Value, 1)   '時分は切り捨てる
                作業内容 = Workbooks(転記元ワークブック名).Sheets(1).Cells(i, 作業内容の列番号).Value
            
                Workbooks(転記元ワークブック名).Sheets(1).Cells(i, 実施箇所の列番号).Interior.Color = 65535
            
                Workbooks(転記先ワークブック名).Activate

                For j = 月初 To 月末
                    If Cells(月行, j).Value >= 開始日 And Cells(月行, j).Value <= 終了日 And TypeName(Cells(月行, j).Value) = "Date" Then
                        If Cells(転記先の行(k), j).Value <> "" Then 'セルが空白でなければ改行し、空白であれば改行しない
                            Cells(転記先の行(k), j).Value = Cells(転記先の行(k), j).Value & vbLf & "--------" & vbLf & 作業内容
                        Else
                            Cells(転記先の行(k), j).Value = 作業内容
                        End If

                    End If
                Next j
                
            End If
        Next i
        
        k = k + 1

    Loop

End Sub