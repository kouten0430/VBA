Sub リストを使用して新規フォルダを作成する()
    Dim mb As Integer
    Dim Dn As String
    Dim myRange As Range
    Dim ct As Long
    
    mb = MsgBox(prompt:="選択中のセルの値をフォルダ名にして、新規フォルダを作成します。", Buttons:=vbYesNo)
        If mb = 7 Then  '「いいえ」を選択した場合はプロシージャを終了する
            Exit Sub
        End If
            
    With Application.FileDialog(msoFileDialogFolderPicker)
        .Title = "新規フォルダを作成する場所（パス）を指定して下さい"
        
        If .Show = -1 Then
            Dn = .SelectedItems(1)
        Else
            Exit Sub
        End If
    
    End With
    
    For Each myRange In Selection.SpecialCells(xlCellTypeVisible)   '可視セルのみに処理を行う
        If myRange.Address = myRange.MergeArea(1).Address Then   '結合セルの場合は左上の値のみ取り出す
            If myRange.MergeArea(1).Value <> "" Then 'セルの値が空白以外の場合のみ処理を行う
                If Dir(Dn & "\" & CStr(myRange.MergeArea(1).Value), vbDirectory) = "" Then '同名フォルダが存在しない場合のみ処理を行う
                    MkDir Path:=Dn & "\" & CStr(myRange.MergeArea(1).Value) '新規フォルダを作成する
                    ct = ct + 1
                End If
            End If
        End If
    Next myRange
    
    MsgBox "処理成功：" & ct & " フォルダ"

End Sub