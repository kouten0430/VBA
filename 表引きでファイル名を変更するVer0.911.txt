Sub 表引きでファイル名を変更する()
    Dim Dn As String
    Dim Tdim As Variant
    Dim ist As Variant
    Dim lp As Variant
    Dim i As Integer
    Dim Fn As String
    Dim p As Integer
    Dim ct As Integer
    
    With Application.FileDialog(msoFileDialogFolderPicker)
        .Title = "リネーム作業を行うフォルダーを選択して下さい"
        
        If .Show = -1 Then
            Dn = .SelectedItems(1)
        Else
            Exit Sub
        End If
    
    End With
    
    Tdim = Application.InputBox(prompt:="左列の文字でファイル名を検索し、一致したファイルには、" & vbCrLf & _
    "右列の文字をファイル名に挿入します。" & vbCrLf & "（検索は部分一致で行われます）", Title:="矩形範囲を選択して下さい", _
    Type:=8) '選択された範囲を二次元配列に格納する
        If TypeName(Tdim) = "Boolean" Then  'キャンセルを押された場合の処理
            Exit Sub
        End If
        
retry:
        
     ist = Application.InputBox(prompt:="1:ファイル名の冒頭" & vbCrLf & "2:ファイル名の末尾（拡張子の前）" & vbCrLf & _
     "3:任意", Title:="文字の挿入位置を選択して下さい", Type:=1)
        If TypeName(ist) = "Boolean" Then  'キャンセルを押された場合の処理
            Exit Sub
        ElseIf ist = 3 Then
            lp = Application.InputBox(prompt:="ファイル名の左から何文字目（の前）に挿入するか指定して下さい。", _
            Title:="数値の入力", Type:=1)
                If TypeName(lp) = "Boolean" Then  'キャンセルを押された場合の処理
                    Exit Sub
                ElseIf lp < 1 Then
                    lp = 1
                End If
        ElseIf ist < 1 Or ist > 3 Then
            MsgBox "1〜3の数値で選んで下さい。"
            GoTo retry
        End If

    For i = 1 To UBound(Tdim)   '二次元配列の一次元の最大値までループ
        If Not IsEmpty(Tdim(i, 1)) Then
            Fn = Dir(Dn & "\*" & Tdim(i, 1) & "*")    'フォルダー内を部分一致条件で検索し、一致したファイル名を取得（なければ空白が返る）
        
            Do Until Fn = ""    'ファイル名が空白になるまでループ
                    If ist = 1 Then
                        p = 1
                    ElseIf ist = 2 Then
                        p = InStrRev(Fn, ".")  '.を後方から検索して最初に見つかった文字位置（先頭からの文字数）を返す
                    ElseIf ist = 3 Then
                        If lp > InStrRev(Fn, ".") Then
                            p = InStrRev(Fn, ".")
                        Else
                            p = lp
                        End If
                    End If
                    
                Name Dn & "\" & Fn As Dn & "\" & Application.WorksheetFunction.Replace(Fn, p, 0, Tdim(i, 2))  '指定位置に、二列目の値を挿入する
                ct = ct + 1
                Fn = Dir()  '検索一致が複数あった場合の、二つ目以降のファイル名を取得（なければ空白が返る）
            
            Loop
        End If
        
    Next i
    
    MsgBox "処理成功：" & ct & " ファイル"

End Sub