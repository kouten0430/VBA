Sub CSV形式でクリップボードに格納()
    'Microsoft Forms 2.0 Object Libraryを参照設定して下さい
    '正方形または長方形のような連続した選択範囲とする
    '結合セルも単一セル同様のCSV形式にします（ただし、結合セルと単一セルを混在すると歪になります）
    Dim i As Long
    Dim j As Long

    For i = Selection.Row To Selection.Rows(Selection.Rows.Count).Row
        For j = Selection.Column To Selection.Columns(Selection.Columns.Count).Column
            If Cells(i, j).Address = Cells(i, j).MergeArea(1).Address Then '結合セルの場合は左上の値のみ取り出す
                If Cells(i, j).MergeArea(1).Address = Cells(i, Selection. _
                Columns(Selection.Columns.Count).Column).MergeArea(1).Address Then
                '選択範囲の最終列（最終列を含む結合セル）であれば末尾に改行を追加
                    V = V & Cells(i, j).Value & vbCrLf
                Else
                '選択範囲の最終列（最終列を含む結合セル）以外は末尾にカンマを追加
                    V = V & Cells(i, j).Value & ","
                End If
            End If
        Next j
    Next i
    
    V = Left(V, Len(V) - 2) '最終行の改行区切りを取り除く（CrLfは2文字）
    
    With New MSForms.DataObject
        .SetText V  '変数の値をDataObjectに格納する
        .PutInClipboard 'DataObjectのデータをクリップボードに格納する
    End With
    
End Sub