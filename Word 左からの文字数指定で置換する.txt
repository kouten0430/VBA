Sub 左からの文字数指定で置換する()
    '選択範囲内にある各段落に対して処理します
    'ReplaceEndが0指定のときは、置換開始位置の左側に挿入されます
    Dim ReplaceStart As Integer
    Dim ReplaceEnd As Integer
    Dim 指定文字 As String
    Dim SelectionStart As Integer
    Dim SelectionEnd As Integer
    Dim 段落 As Paragraph
    
    ReplaceStart = InputBox("左から何文字目を置換開始位置にしますか？")
        If ReplaceStart < 1 Then
            MsgBox "1未満は指定できません"
            Exit Sub
        End If
    ReplaceEnd = InputBox("開始位置から何文字分を置換しますか？")
        If ReplaceEnd < 0 Then
            MsgBox "0未満は指定できません"
            Exit Sub
        End If
    
    指定文字 = InputBox("置換後の文字を入力して下さい。")
        If 指定文字 = "" Then
            Exit Sub
        End If

    'Textプロパティで選択が解除されるので、選択範囲を記憶させる
    SelectionStart = Selection.Start
    SelectionEnd = Selection.End
    
    For Each 段落 In ActiveDocument.Range(SelectionStart, SelectionEnd).Paragraphs
        If 段落.Range.Start + ReplaceStart - 1 < 段落.Range.End - 1 Then    '開始位置が段落の文字数を超過している場合は処理しない
            If 段落.Range.Start + ReplaceStart + ReplaceEnd - 1 <= 段落.Range.End - 1 Then
                ActiveDocument.Range(段落.Range.Start + ReplaceStart - 1, _
                段落.Range.Start + ReplaceStart + ReplaceEnd - 1).Text = 指定文字
            Else    '開始位置からの文字数が段落の範囲を超える場合、強制的に範囲内で収める
                ActiveDocument.Range(段落.Range.Start + ReplaceStart - 1, _
                段落.Range.End - 1).Text = 指定文字
            End If
        End If
    Next
    
End Sub