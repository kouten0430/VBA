Sub 複数ブックのデータを一つのシートに連結()
    Dim awn As String
    Dim Bcdrive As String
    Dim Bcdir As String
    Dim lwfn As Variant
    Dim ctm As Integer
    Dim yr As Range
    Dim nod As Variant
    Dim anod As Long
    Dim y As Long
    Dim x As Integer
    Dim myWb As Variant
    Dim i As Integer
    Dim lwn As String
    Dim myLis As ListObject
    Dim lye As Long
    Dim aye As Long
    Dim ct As Integer
    
    awn = ActiveWorkbook.Name
        If Not awn Like "*.*" Then
            MsgBox "新規ブック上では実行できません。"
            Exit Sub
        End If
        
    Bcdrive = Left(CurDir, InStr(CurDir, ":") - 1)  '現在のカレントドライブを記憶
    Bcdir = CurDir  '現在のカレントディレクトリを記憶
    
    ChDrive Left(ActiveWorkbook.Path, InStr(ActiveWorkbook.Path, ":") - 1)    'カレントドライブを一時的に変更
    ChDir ActiveWorkbook.Path 'カレントディレクトリを一時的に変更
    
    lwfn = Application.GetOpenFilename(Title:="コピーするブックを選択（複数選択可）", MultiSelect:=True)
        If TypeName(lwfn) = "Boolean" Then  'キャンセルを押された場合の処理
            ChDrive Bcdrive
            ChDir Bcdir
            Exit Sub
        End If
        
    ctm = UBound(lwfn)

retry1:

    On Error Resume Next
    
    Set yr = Nothing 'retryした時のためのリセット

    Set yr = Application.InputBox(Prompt:="コピーの始点となる行のセルを選択し、OKして下さい。", Type:=8)
        If yr Is Nothing Then    'キャンセルを押された場合の処理
            ChDrive Bcdrive
            ChDir Bcdir
            Exit Sub
        ElseIf yr.Resize(1, 1).Value = "" Then  '複数範囲を選択された場合も想定して、Resizeを適用する
            MsgBox "空白以外のセルを選択してください。"
            GoTo retry1
        End If

    On Error GoTo 0
    
retry2:
    
    nod = Application.InputBox(Prompt:="行×列 に含まれる、最大データ数を指定して下さい。" & vbCrLf & _
    "・コピーするブックがこの値を超えた場合、処理をスキップします。" & vbCrLf & _
    "・フリーズ防止策です。環境によって可変して下さい。", Default:="10000", Type:=1)
        If TypeName(nod) = "Boolean" Then  'キャンセルを押された場合の処理
            ChDrive Bcdrive
            ChDir Bcdir
            Exit Sub
        Else
            anod = WorksheetFunction.CountIf(Range("A1:XFD1048576"), "<>")
            If anod > nod Then
                MsgBox "ペーストされる当ブックに" & Format(nod, "#,###") & "を超える、" & Format(anod, "#,###") & "データが存在します。" _
                & vbCrLf & vbCrLf & "フリーズを避けるために、軽量なブック上で実行することを推奨します。" & vbCrLf & _
                "コピーするブックではさらにメモリを必要としますので、必要に応じ、" & vbCrLf & "最大データ数を可変して下さい。"
                GoTo retry2
            End If
        End If
    
    Application.ScreenUpdating = False  '画面表示の更新をオフにする
    
    y = yr.Row  'yrから行を取得
    x = yr.Column   'yrから列を取得
    
    If Not ActiveSheet.AutoFilter Is Nothing Then   '念のための処理１
        Cells(y, x).AutoFilter
    End If

    For Each myWb In lwfn
        For i = 1 To Workbooks.Count
            If Workbooks(i).FullName = myWb Then    '既に開いているブックは処理をスキップ
                GoTo skip
            End If
        Next i
        
        Workbooks.Open Filename:=myWb 'ブックを開くと同時にアクティブとなる
        lwn = ActiveWorkbook.Name   'パスを含まないブック名を取得しておく
        
        If ActiveSheet.ListObjects.Count > 0 Then   '念のための処理２
            For Each myLis In ActiveSheet.ListObjects
                myLis.Unlist
            Next myLis
        End If
        
        If Not ActiveSheet.AutoFilter Is Nothing Then   '開いたブックのシートにオートフィルターが設定されている場合は解除する
            Cells(y, x).AutoFilter
        End If
        
        lye = Cells(y, x).CurrentRegion.Rows(Cells(y, x).CurrentRegion.Rows.Count).Row
            
        If WorksheetFunction.CountIf(Rows(y & ":" & lye), "<>") <= nod Then '指定した最大値以下の場合のみ処理する
        
            Rows(y & ":" & lye).Copy
        
            Workbooks(awn).Activate
            aye = Cells(y, x).CurrentRegion.Rows(Cells(y, x).CurrentRegion.Rows.Count).Row
            Rows(aye + 1).Insert
        
            Workbooks(lwn).Application.CutCopyMode = False  'コピーモードを解除する
            
        End If
        
        Workbooks(lwn).Close savechanges:=False
        DoEvents    'Closeが完了するまで待つ

skip:

        ct = ct + 1
        Application.StatusBar = "処理実行中．．．" & ct & "/" & ctm
        
    Next myWb
    
    ChDrive Bcdrive
    ChDir Bcdir
    
    Application.StatusBar = False

End Sub