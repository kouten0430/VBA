Sub 表引きでファイル名を変更する()
    Dim Bcdrive As String
    Dim Bcdir As String
    Dim Tdim As Variant
    Dim i As Integer
    Dim Fn As String
    Dim pp As Integer
    Dim LFn As String
    Dim RFn As String
    
    Bcdrive = Left(CurDir, InStr(CurDir, ":") - 1)  '現在のカレントドライブを記憶
    Bcdir = CurDir  '現在のカレントディレクトリを記憶
    
    ChDrive Left(ActiveWorkbook.Path, InStr(ActiveWorkbook.Path, ":") - 1)    'カレントドライブを一時的に変更
    ChDir ActiveWorkbook.Path 'カレントディレクトリを一時的に変更
    
    Tdim = Application.InputBox(prompt:="表引きする範囲を選択", Type:=8)    '選択された範囲を二次元配列に格納する
        If TypeName(Tdim) = "Boolean" Then  'キャンセルを押された場合の処理
            ChDrive Bcdrive
            ChDir Bcdir
            Exit Sub
        End If

    For i = 1 To UBound(Tdim)   '二次元配列の一次元の最大値までループ
        Fn = Dir("*" & Tdim(i, 1) & "*")    'CD内を部分一致条件で検索し、一致したファイル名を取得（なければ空白が返る）
        
        Do Until Fn = ""    'ファイル名が空白になるまでループ
            pp = InStrRev(Fn, ".")  '.を後方から検索して最初に見つかった文字位置（先頭からの文字数）を返す
            LFn = Left(Fn, pp - 1)  '拡張子を除くファイル名
            RFn = Right(Fn, Len(Fn) - (pp - 1))   '拡張子
            Name Fn As LFn & "_" & Tdim(i, 2) & RFn '元のファイル名と拡張子の間に、二列目の値を追記する形でファイル名を変更する
            Fn = Dir()  '検索一致が複数あった場合の、二つ目以降のファイル名を取得（なければ空白が返る）
        Loop
        
    Next i
        
    ChDrive Bcdrive
    ChDir Bcdir

End Sub